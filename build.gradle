import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'dev.rosewood.mojang-remapper' version '1.0.0'
}

group 'dev.rosewood'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    api 'dev.rosewood:rosegarden:1.0.7-SNAPSHOT'

    compileOnly 'org.spigotmc:spigot:1.17:remapped-mojang'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'

    mojangToObf 'org.spigotmc:minecraft-server:1.17:maps-mojang@txt'
    mojangRemapped 'org.spigotmc:spigot:1.17:remapped-mojang'
    obfToRuntime'org.spigotmc:minecraft-server:1.17:maps-spigot@csrg'
    obfRemapped 'org.spigotmc:spigot:1.17:remapped-obf'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null);
    relocate('dev.rosewood.rosegarden', 'dev.rosewood.rosechat.lib.rosegarden')
    minimize()
}

build.dependsOn shadowJar, runtimeMappedJar
