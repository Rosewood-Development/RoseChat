import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'dev.rosewood'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
    maven { url = 'https://m2.dv8tion.net/releases' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }

}

dependencies {
    api 'dev.rosewood:rosegarden:1.0.7'

    compileOnly 'org.spigotmc:spigot:1.17.1:remapped-mojang'
    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'com.discordsrv:discordsrv:1.23.0'
    compileOnly 'net.dv8tion:JDA:4.2.1_262'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null);
    relocate('dev.rosewood.rosegarden', 'dev.rosewood.rosechat.lib.rosegarden')
    minimize()
}

build.dependsOn shadowJar
