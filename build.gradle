import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

group 'dev.rosewood'
version 'Beta-24'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
    maven { url = 'https://m2.dv8tion.net/releases' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }

    maven { url = 'https://repo.glaremasters.me/repository/towny/' }
    maven { url = 'https://nexus.neetgames.com/repository/maven-releases/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://repo.roinujnosde.me/releases/' }
    maven { url = 'https://ci.ender.zone/plugin/repository/everything/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.bg-software.com/repository/api/' }
    maven { url = 'https://nexus.iridiumdevelopment.net/repository/maven-releases/' }
    maven { url = 'https://repo.pcgamingfreaks.at/repository/maven-everything/' }
    maven { url = 'https://repo.essentialsx.net/releases/' }
}

dependencies {
    api 'dev.rosewood:rosegarden:1.4.0-SNAPSHOT'
    api 'org.apache.commons:commons-text:1.12.0'

    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'com.discordsrv:discordsrv:1.27.0'
    compileOnly 'net.dv8tion:JDA:4.3.0_349'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.1.0'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0-SNAPSHOT'

    // Plugin Support
    compileOnly 'com.palmergames.bukkit.towny:towny:0.100.2.0'
    compileOnly 'com.gmail.nossr50.mcMMO:mcMMO:2.2.012'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.0'
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:7.3.0'
    compileOnly 'net.sacredlabyrinth.phaed.simpleclans:SimpleClans:2.19.2'
    compileOnly 'com.massivecraft:Factions:1.6.9.5-U0.6.29'
    compileOnly 'com.github.cryptomorin:kingdoms:1.16.3.3'
    compileOnly 'world.bentobox:bentobox:2.3.0-SNAPSHOT'
    compileOnly 'com.bgsoftware:SuperiorSkyblockAPI:2024.1'
    compileOnly 'com.songoda:skyblock:2.5.0'
    compileOnly 'at.pcgamingfreaks:MarriageMaster-API-Bukkit:2.7.5'
    compileOnly 'net.essentialsx:EssentialsX:2.20.1'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null);
    relocate('dev.rosewood.rosegarden', 'dev.rosewood.rosechat.lib.rosegarden')
    relocate('org.apache', 'dev.rosewood.rosechat.lib.apache')
    minimize()
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = rootProject.name.toLowerCase()
            pom {
                name = rootProject.name.toLowerCase()
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
publishShadowPublicationToMavenRepository.dependsOn jar
